clear all
set more off

*5 Some verification for question 5
capture program drop bittest

/*
This program below does ttest for Bernoulli, it 
takes in sample mean: xbar, population mean: mu
calculate std error: se, test stats: tn, and p-value for both one-sided and two-sided test
*/
program bittest
	local xbar = `1'
	local mu = `2'
	display "ttest with sample mean: `xbar', mu: `mu'"
	*local se = sqrt(`xbar'*(1-`xbar')/500)
	local se = sqrt(`mu'*(1-`mu')/500)
	local tn = abs(`xbar'-`mu')/`se'
	local pval_onesided = 1-normal(`tn')
	local pval_twosided = (1-normal(`tn'))*2
	display "tn is `tn', se is `se', one sided pval is `pval_onesided', two sided pval is `pval_twosided'"
end 

bittest 226/500 0.5 
bittest 185/500 0.4 
bittest 131/500 0.3 

*Verify with bitesti 
bitesti 500 226 0.5 //Bryan
bitesti 500 185 0.4 //Jonathan
bitesti 500 131 0.3 //Margaret

*6. This problem asks you to conduct a series of sampling experiments using Stata
/*(a) Draw 500 random samples with sample size of N ∈ {8, 32, 128} from a Normal distribution (so you draw 500 random sample three times, each time with a different N). Plot histograms of the sampling distributions of the sample mean for each of these three sample sizes. Repeat experiments and plots for three samples drawn from a Bernoulli distribution with parameter p (your choice of value). 
(b) Compute the mean and variance of the 500 sample means generated by each experiment and compare them to the sampling distribution mean and variance predicted by statistical theory. Does sampling variance decrease with sample size at the rate predicted by the theory? Does Normality of the underlying data matter for this?
(c) Compute the empirical coverage rate for 95% confidence intervals in each of your experiments. To what extent does coverage accuracy depend on Normality?
*/
/*
Simple program to simulate a distribution : Normal or Bernoulli, and particular sample size i
It generates histogram and also test for Confidence Interval coverage rate for alpha = 5%.
arg1: distribution: a string of either "norm" or "ber"
arg2: sample size: n
arg3: # of simulations to draw from: s
*/
capture program drop simulation
program simulation
	* Set set for reproducibility
	set seed 456

	local distr = "`1'"
	local n = `2'
	local s = `3'
	display "Simulaton run for distribution: `distr', sample size: `n', sim num `s'"
	
	foreach j of numlist 1/`n'{
		qui set obs `s'
		if ("`distr'" == "norm") {
			gen sim_`j' = rnormal()
			local th_mu = 0
			local th_se = 1/sqrt(`n')
		}
		else {
			gen sim_`j' = rbinomial(1, 0.7)
			local th_mu = 0.7
			local th_se = sqrt((0.7*0.3)/`n')
		}
	}
	
	egen sample_mean = rowmean(sim_*) // computes row-wise mean, 1x500
	egen sample_sd = rowsd(sim_*) // computes row-wise sd, 1x500
	gen sample_se = sample_sd / sqrt(`n') //standard error for both scaling and CI 
		
	* Plot histogram with normal fit
	qui hist sample_mean, title("Sample size `n' from `distr'") normal
	qui graph export hist`n'_`distr'.pdf, replace
	
	*mean and variance of the sampling distribution compared to theoretical stats
	sum sample_mean
	display "mean is: `r(mean)', se is: `r(sd)'"
	display "theoretical mean is: `th_mu', theoretical se is: `th_se'" 

	*construct confidence intervals and dummy if pop mean is contained 
	local ca = invnormal(0.975) //critial value 2 sides at alpha = 5%
	gen ci_low = sample_mean - `ca'*sample_se
	gen ci_high = sample_mean + `ca'*sample_se
	gen ci_contains = (ci_low <=`th_mu') & (ci_high >= `th_mu')
	sum ci_contains
	display "coverage rate is: `r(mean)'" 

	drop sim_* sample* ci_*
end

foreach i in 8 32 128 {
	simulation norm `i' 500
}

foreach i in 8 32 128 {
	simulation ber `i' 500
}

*B. Empirical Warm-up

*(a) For each experiment, test the hypothesis that bonuses decreased the proportion of UI claimants who exhausted their benefits using a one-tailed test. Compute the standard error needed for the denominator of the test statistic for two scenarios: (i) the experiment has an effect and (ii) the experiment has no effect.


* For the employer experiment, test the hypothesis that bonuses affected weeks of insured unemployment in the first unemployment spell using one-tailed and two-tailed 1% tests (compute standard errors allowing for an effect). Explain why statistical power considerations might cause you to prefer one or the other of these two tests.

/* Create my own program of 2 sample t-test, which takes in the following
	xbar_c : control sample mean
	se_c: control se
	xbar_c: treatment sample mean
	se_t: treatment se
	alpha: size of the test
	This program will do both 1 sided and 2 sided test at alpha, output T stats and pvalue for both tests
*/ 

program twosamplettest
	local xbar_c = `1'
	local se_c = `2'
	local xbar_t = `3'
	local se_t = `4'
	local a = `5'

	local xbar_diff = abs(`xbar_t'-`xbar_c')
	local se_diff = sqrt(`se_c'^2+`se_t'^2)
	local tn = `xbar_diff'/`se_diff'
	local ca_onesided = invnormal(`a'/2)
	local ca_twosided = invnormal(`a')

	local pval_onesided = 1-normal(`tn')
	local pval_twosided = 2*`pval_onesided'
	
	display "mean diff is `xbar_diff', se of diff: `se_diff', test stats tn is `tn'"
	display "P-value of one tailed tests is `pval_onesided', two tailed test is `pval_twosided'"
end 

*example: 2 sample ttest for weeks of insured unemployment at first spell
twosamplettest 18.3 0.205 17.7 0.205 0.01




*C. Working with NHIS data

*** Metrics
*** Table 1.1

* by Georg Graetz, August 6, 2013
* modified by Gabriel Kreindler, June 13, 2014
* modified by Jon Petkun, January 2, 2015
* modified by Ryan Hill, Jan 31, 2020
* modified by Hellary Zhang, April 5, 2021
* modified by Can Yeşildere, February 10, 2023


// set to directory where NHIS2009_clean.dta is stored
*cd 

*cap log using NHIS2009_hicompare.log, text replace


* Here's a simple program to take in the raw NHIS dataset and create separate datafiles for husbands and wives, based on our criteria. 

program data_import_clean
	use NHIS2009_clean, clear

* Here are the labels for the variables you need for Table 1.1:
	label variable hlth "Health index 1-5"
	label variable nwhite "Nonwhite"
	label variable age "Age"
	label variable yedu "Education (years)"
	label variable famsize "Family size"
	label variable empl "Employed"
	label variable inc "Family income"
	label variable hi "Health insurance status" 

* PART I: Keep couples and select sample
	
	* select non-missing HI respondents with one female in HH
	* respondents are considered as having insurance if their spouse does
		keep if marradult==1 & perweight!=0 
		keep if hi!=.   
		by serial: egen numfem = total(fml)
			keep if numfem==1
			drop numfem
		
	* MM T1.1 sample selection criteria	
		gen T11 = ( age>=26 & age<=59 & marradult==1 & adltempl>=1 )
			keep if T11==1
		// drop single-person HHs
		by serial: gen n = _N
			keep if n>1
		
* PART II: Create different datasets for husbands and wives
	
	preserve
		keep if fml == 0 
		save husbands.dta, replace
		sum hlth hi age marstat sex famsize relate racenew educ 
	restore	
		keep if fml == 1
		save wives.dta, replace
		sum hlth hi age marstat sex famsize relate racenew educ
end

/* C. Working with NHIS Data
Does health insurance keep you healthy? Table 1.1 in Mastering 'Metrics compares the health and demo- graphic characteristics of married couples with and without health insurance. Health is measured on a scale of 1-5, with 5 being the healthiest.
1. Using the information in the published table, calculate the t-statistic for the null hypothesis that there is no difference between the health of husbands who have health insurance and the health of those who don't. Is this difference significantly different from zero?
*/
local tn = 0.31/0.03
local pval_twosided = 2*(1-normal(`tn'))
display "pval 2 sided is: `pval_twosided'"
*P-value is 0, reject at both 1% and 5%, therefore the difference of health between insured and uninsured husband is significant. 

* The next task is to recreate Table 1.1!

* (a) Replicate the first 3 columns of MM Table 1.1 (which compares statistics for husbands)

* t-test between Some HI vs No HI groups, on differences of the following: health index, nonwhite, age, education, family size, employed, family income
data_import_clean
use husbands.dta, clear

*unweighted 
estpost ttest hlth nwhite age educ famsize empl inc, by(hi)
esttab ., cells("mu_2 mu_1 b se p")


* For learning and demo, this is for weighted differences
svyset [pw = perweight]
foreach col in hlth nwhite age yedu famsize empl inc {
	qui svy: mean `col', over(hi)
	estat sd
	svy: regress `col' hi
}

* (b) Restrict your sample to employed college graduates (those with 16+ years of schooling). How does this restriction change the health gap by insurance status?
capture drop employed_college
gen employed_college = (empl==1) & (yedu>=16)
qui svy, subpop(employed_college): mean hlth, over(hi)
estat sd
svy, subpop(employed_college): regress hlth hi

*compare to everyone
qui svy: mean hlth, over(hi)
estat sd

* (c) Show that employed college graduates are generally healthier than the rest of the husband sample and that the insured are more likely than the uninsured to be employed college graduates. How do these facts help interpret the change in insurance gaps as you move from comparison (a) to comparison (b)?

*generally healthier
svy: mean hlth, over(employed_college)
svy: regress hlth employed_college

svy: mean employed_college, over(hi)
svy: regress employed_college hi
* As we can see only about 9% of uninsured men are college educated, but about 39% of insured men are colledge educated. 
* This shows that employment and education could be another channel impacting health status, employed educated are both more likely to be insured and also more likely to be healthier, therefore the healthgap could be due to employment and education rather than health insurance status. In short, employment and education are the confounders in the study of effect on health from insurance. 

tabstat hlth, by(employed_college)
